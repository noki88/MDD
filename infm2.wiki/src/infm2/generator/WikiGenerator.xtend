/*
 * generated by Xtext
 */
package infm2.generator

import infm2.wiki.Site
import infm2.wiki.Wiki
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IFileSystemAccess
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.naming.IQualifiedNameProvider
import javax.inject.Inject
import infm2.wiki.TextBrick
import infm2.wiki.Text
import infm2.wiki.Link

/**
 * «» with ctrl< and ctrl,shift,<
 */
class WikiGenerator implements IGenerator {
	
	@Inject extension IQualifiedNameProvider
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		for(e: resource.allContents.toIterable.filter(Wiki))
		{
			fsa.generateFile("\\" + e.name + "\\site.css", e.css)
			for(f: e.sites) {
				fsa.generateFile("\\" + e.name + "\\" + f.name + ".html", compile(e, f))
			}
			//fsa.generateFile(e.name + "Index.html", e.compile)
		}
	}
	
	
	def compile(Wiki w, Site s) '''
	<!DOCTYPE html>
	<html>
		<head>
			<link rel="stylesheet" href="site.css">
			<title>«s.name»</title>
		</head>
		
		<body>
			<div id="header">
				<h1>«w.name»</h1>
			</div>
			<div id="wrap">
				«/*Navigation*/»
				<div id="nav">
					<ul>
						«FOR ss: w.sites»
							<li><a href="«ss.compileSiteName»">«ss.name»</a></li>
						«ENDFOR»
					</ul>
				</div>
				«/*Text*/»
				<div id="section">
					<h1>«s.name»</h1>
					«FOR p: s.pragraphs»
						<h2>«p.name»</h2>
						<p>
							«FOR t: p.text»
								«t.compile»
							«ENDFOR»
						</p>
					«ENDFOR»
				</div>
			</div>
		
			<div id="footer">
				Copyright «w.copyright.copyright2»
			<div>
		</body>
	</html>
	'''
	
	def dispatch compile(Text t) '''
		«t.name»
	'''
	
	def dispatch compile(Link l)'''
		<a href="«l.siteLink.compileSiteName»">«l.name»</a>
	'''
	
	def compileSiteName(Site s)'''
		«s.name».html
	'''
	
	def css(Wiki w) '''
	
		#header   {
			margin:0px auto;
			width:100%;
			height: 100px;
			background-color:«w.color.color2»;
			color:white;
			text-align:center;
			padding:5px;
		}
		#wrap {
			height:400px;
		}
		#nav {
			line-height:30px;
			background-color:#eeeeee;
			height:100%;
			float:left;
			padding:5px;
		}
		#section {
			width:350px;
			float:left;
			padding:10px;
		}
		#footer {
			background-color:black;
			color:white;
			clear:both;
			text-align:center;
			padding:5px;
		}'''
}
